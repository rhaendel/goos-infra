plugins {
	id 'com.bmuschko.docker-remote-api' version '4.2.0'
}


import com.bmuschko.gradle.docker.tasks.DockerInfo
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

repositories {
	jcenter()
}

ext {
	dockerBaseDir = file("$buildDir/docker").absolutePath
}

task dockerInfo(type: DockerInfo)

task copySrcFiles(type: Copy) {
	from 'src'
	into dockerBaseDir
}

task createDockerfile(type: Dockerfile) {
	destFile = project.file("$dockerBaseDir/Dockerfile")
	from 'java:8'
	runCommand 'apt-get update && apt-get install wget'
	//runCommand "wget 'http://www.igniterealtime.org/downloads/download-landing.jsp?file=openfire/openfire_3_6_0a.tar.gz' "
	runCommand "wget 'https://github.com/igniterealtime/Openfire/releases/download/v3.6.0a/openfire_3_6_0a.tar.gz' -O openfire_3_6_0a.tar.gz "
	runCommand 'tar xzf openfire_3_6_0a.tar.gz'
	copyFile 'openfire.xml', '/openfire/conf/openfire.xml'
	copyFile 'entrypoint.sh', '/entrypoint.sh'
	defaultCommand 'sh -ex /entrypoint.sh'.split()
	exposePort 5222, 9090
}

task buildImage(type: DockerBuildImage) {
	dependsOn copySrcFiles
	dependsOn createDockerfile
	inputDir = file(createDockerfile.destFile).parentFile
	tags.add('tma/goos-openfire')
}

task createContainer(type: DockerCreateContainer) {
	containerName = "goos-openfire"
	portBindings = ['5222:5222', '9090:9090']
	targetImageId { "tma/goos-openfire" }
}

task startContainer(type: DockerStartContainer) {
	targetContainerId { "goos-openfire" }
}
task stopContainer(type: DockerStopContainer) {
	targetContainerId { "goos-openfire" }
}
task removeContainer(type: DockerRemoveContainer) {
	targetContainerId { "goos-openfire" }
}
